import Mathlib.NumberTheory.Padics.PadicNumbers
import Mathlib.Algebra.Order.Floor
import Mathlib.Data.Nat.Log
import Mathlib.InformationTheory.Hamming
import Mathlib.Data.Finset.Basic

set_option tactic.hygienic false

/-!

  # VC-dimensions of nondeterministic finite automata for words of equal length
    Part 1: Define VC dimension and VC index and prove
    `dimVC = indexVC - 1` (even in the case of the empty family)

    Definition 10, Lemma 11, and Lemma 12 from the paper.
    (Bonus material for ac-exercises)

-/
section Basics
variable {V : Type*} [DecidableEq V] [Fintype V]

/-- To make this computable, restrict A âŠ† â‹ƒ ğ“• -/
def shatters_some (ğ“• : Finset (Finset V)) (d : â„•) : Prop :=
    âˆƒ A, A.card = d âˆ§ âˆ€ B âŠ† A, âˆƒ C âˆˆ ğ“•, A âˆ© C = B

def shatters_all (ğ“• : Finset (Finset V)) (d : â„•) : Prop :=
    âˆ€ A, A.card = d â†’ âˆ€ B âŠ† A, âˆƒ C âˆˆ ğ“•, A âˆ© C = B

instance (A B C : Finset V) : Decidable (A âˆ© C = B) := decEq (A âˆ© C) B

instance (ğ“• ğ“– : Finset (Finset V)) : Decidable (ğ“• = ğ“–) := decEq ğ“• ğ“–

instance (A B : Finset V) (ğ“• : Finset (Finset V)) : Decidable (âˆƒ C âˆˆ ğ“•, A âˆ© C = B) := by
  exact Multiset.decidableExistsMultiset

instance (A : Finset V) (ğ“• : Finset (Finset V)) : Decidable (âˆ€ B âŠ† A, âˆƒ C âˆˆ ğ“•, A âˆ© C = B) := by
  exact Fintype.decidableForallFintype

instance (A : Finset V) (ğ“• : Finset (Finset V)) (d : â„•) : Decidable (A.card = d âˆ§ âˆ€ B âŠ† A, âˆƒ C âˆˆ ğ“•, A âˆ© C = B) := by
  exact instDecidableAnd

instance (ğ“• : Finset (Finset V)) (d : â„•) : Decidable (shatters_some ğ“• d) := by
  unfold shatters_some
  exact Fintype.decidableExistsFintype

instance (ğ“• : Finset (Finset V)) (d : â„•) : Decidable (shatters_all ğ“• d) := by
  unfold shatters_all
  exact Fintype.decidableForallFintype


end Basics

theorem empty_does_not_shatter {V : Type*} [DecidableEq V] (k : â„•) :
    Â¬ shatters_some (âˆ… : Finset (Finset V)) k :=
  fun âŸ¨s,hsâŸ© => by
    simp at hs
    obtain âŸ¨_, _âŸ© := hs.2 âˆ… (Finset.empty_subset s)

theorem nonempty_shatters {V : Type*} [DecidableEq V]  (ğ“• : Finset (Finset V)) {A : Finset V} (hA : A âˆˆ ğ“•) :
    shatters_some ğ“• 0 := by
  use âˆ…
  simp
  intro B hB
  constructor
  use A
  symm
  exact Finset.subset_empty.mp hB

open Finset


theorem equivVC {V : Type*} [DecidableEq V] [Fintype V] (F: Finset (Finset V)) :
    0 < F.card â†” shatters_some F 0 := by
  constructor
  Â· intro h
    have hâ‚€ : F.card â‰  0 := Nat.not_eq_zero_of_lt h
    have : F â‰  âˆ… := Ne.symm (ne_of_apply_ne card fun a â†¦ hâ‚€ (a.symm))
    exact nonempty_shatters F (nonempty_iff_ne_empty.mpr this).exists_mem.choose_spec
  Â· intro h
    suffices F â‰  âˆ… by
      have : F.card â‰  0 := by
        apply card_ne_zero.mpr
        exact nonempty_iff_ne_empty.mpr this
      exact Nat.zero_lt_of_ne_zero this
    intro hc
    exact empty_does_not_shatter (V := V) (k := 0) <| hc â–¸ h

/-- The VC dimension of a finite set family. -/
def dimVC {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) : â„• := by
  by_cases H : ğ“• = âˆ…
  exact 0 -- dummy value, should really be -1 but that is inconvenient in Lean
  exact Finset.max'
    (filter (shatters_some ğ“•) (range (ğ“•.card + 1))) (by
      use 0
      simp
      refine (equivVC ğ“•).mp ?h.a
      refine card_pos.mpr ?h.a.a
      exact nonempty_iff_ne_empty.mpr H
      )

def dimVC! {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) : â„• := by
  by_cases H : ğ“• = âˆ…
  exact 0 -- dummy value, should really be -1 but that is inconvenient in Lean
  exact Finset.max'
    (filter (shatters_some ğ“•) (range ((univ : Finset V).card + 1))) (by
      use 0
      simp
      refine (equivVC ğ“•).mp ?h.a
      refine card_pos.mpr ?h.a.a
      exact nonempty_iff_ne_empty.mpr H
      )

open Finset

/-- Obtain a smaller subset of a given set. -/
theorem subset_of_size {Î± : Type*} {s : Finset Î±} (a b : â„•)
    (h_card : s.card = b) (h_le : a â‰¤ b) :
    âˆƒ t : Finset Î±, t âŠ† s âˆ§ t.card = a := by
  classical
  -- Extract the list representation of `s`
  let l := s.toList
  have h_length : l.length = b := by
    have := @List.toFinset_card_of_nodup Î± _ l (nodup_toList s)
    aesop
  -- Take the first `a` elements of the list
  let l' := l.take a

  -- Convert the list back into a finset
  let t := l'.toFinset

  -- Prove that `t` is a subset of `s`
  have h_subset : t âŠ† s := by
    intro x hx

    rw [List.mem_toFinset] at hx
    have : x âˆˆ List.take a s.toList := hx
    simp at this
    refine mem_toList.mp ?_
    exact List.mem_of_mem_take hx

  -- Prove that the cardinality of `t` is `a`
  have h_card_t : t.card = a := by
    rw [Finset.card_def]

    show (List.take a s.toList).toFinset.val.card = a
    have : a â‰¤ s.card := by aesop
    simp
    have : #s.toList.toFinset = l.length := @List.toFinset_card_of_nodup Î± _ l (nodup_toList s)
    have := @List.length_take Î± a s.toList
    have : (List.take a s.toList).dedup = (List.take a s.toList) :=
      ((s.toList.take_sublist a).nodup <| nodup_toList s).dedup

    rw [this]
    simp
    tauto

  -- Combine the results
  exact âŸ¨t, h_subset, h_card_tâŸ©

lemma of_size_subset (V : Type*) [Fintype V] (S : Finset V) (k l : â„•) (hâ‚€ : k â‰¤ l)
    (h : Finset.card S = l) : âˆƒ T, Finset.card T = k âˆ§ T âŠ† S := by
  have := @subset_of_size V S k l h hâ‚€
  aesop

/-- A family can also shatter a smaller set. -/
lemma shatters_monotone {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• :  Finset (Finset V)) (hğ“• : âˆƒ f, f âˆˆ ğ“•) (k l : â„•) (h : k â‰¤ l)
    (hâ‚€ : shatters_some ğ“• l) : shatters_some ğ“• k := by
  obtain âŸ¨Aâ‚€,hAâ‚€âŸ© := hâ‚€
  obtain âŸ¨A,hAâŸ© := of_size_subset V  Aâ‚€ k l h hAâ‚€.1
  use A
  simp_all
  intro B hB
  have := hAâ‚€.2 (B) (by tauto)
  obtain âŸ¨Câ‚€,hCâ‚€âŸ© := this
  use Câ‚€
  simp_all
  ext x
  constructor
  Â· intro H
    simp at H
    rw [â† hCâ‚€.2]
    simp
    tauto
  Â· intro H
    simp_all
    constructor
    Â· tauto
    Â· rw [â† hCâ‚€.2] at H
      simp at H
      tauto


lemma le_max'_iff (S : Finset â„•) (h : S.Nonempty) (k : â„•) :
  k â‰¤ S.max' h â†” âˆƒ y âˆˆ S, k â‰¤ y := le_sup'_iff _

-- #eval @dimVC (Fin 2) _ _ âŸ¨{{0}},by simpâŸ©
-- #eval @dimVC (Fin 2) _ _ âŸ¨{{0,1}},by simpâŸ©
-- #eval @dimVC (Fin 2) _ _ âŸ¨{{1}},by simpâŸ©
-- #eval @dimVC (Fin 2) _ _ âŸ¨{âˆ…},by simpâŸ©

theorem shattering_skolem {V : Type*} [DecidableEq V] [Fintype V] (F : Finset (Finset V)) (k : â„•)
    (h : shatters_some F k) :
    âˆƒ A : Finset V, A.card = k âˆ§ âˆƒ Ï† : {B // B âŠ† A} â†’ {C // C âˆˆ F},
        âˆ€ B : {B // B âŠ† A}, A âˆ© (Ï† B) = B :=
    Exists.elim h (fun A hâ‚€ => âŸ¨A, hâ‚€.1, Exists.intro
        (fun B => âŸ¨(hâ‚€.2 B.1 B.2).choose, (hâ‚€.2 B.1 B.2).choose_spec.1âŸ©)
         fun B =>  (hâ‚€.2 B.1 B.2).choose_spec.2âŸ©)

theorem VC_injective_function {V : Type*} [DecidableEq V] [Fintype V] (F : Finset (Finset V)) (A : Finset V)
    (Ï† : {B // B âŠ† A} â†’ {C : Finset V // C âˆˆ F})
    (h : âˆ€ B : {B // B âŠ† A}, A âˆ© (Ï† B) = B) :
    Function.Injective Ï† :=
  fun Bâ‚ Bâ‚‚ h0 => by
  have h3: A âˆ© (Ï† Bâ‚) = A âˆ© (Ï† Bâ‚‚) := by rw [h0]
  have : Bâ‚.val = Bâ‚‚.val := Eq.trans (Eq.trans (h Bâ‚).symm h3) (h Bâ‚‚)
  cases Bâ‚‚
  cases Bâ‚
  dsimp at *
  induction this
  rfl

/-- Lean 3 version thanks to Adam Topaz. -/
theorem card_of_injective {V : Type*} [DecidableEq V] [Fintype V] (F : Finset (Finset V)) (A : Finset V)
    (Ï† : {B // B âŠ† A} â†’ {C : Finset V // C âˆˆ F})
    (h : Function.Injective Ï†) : A.powerset.card â‰¤ F.card := by

  have hâ‚: #A.powerset = Fintype.card { B // B âŠ† A } := by
    refine Eq.symm (Fintype.card_of_subtype A.powerset ?H)
    simp only [mem_powerset, implies_true]
  rw [hâ‚, â† Fintype.card_coe]
  exact Fintype.card_le_of_injective Ï† h

theorem pow_le_of_shatters {V : Type*} [DecidableEq V] [Fintype V] (F : Finset (Finset V)) (k : â„•)
  (h : shatters_some F k) : 2^k â‰¤ F.card :=
  Exists.elim (shattering_skolem F k h) (fun A g => Exists.elim g.2 (fun Ï† hphi =>
      calc
           _ = 2^A.card := by rw [â† g.left]
           _ = A.powerset.card := (card_powerset A).symm
           _ â‰¤ F.card := card_of_injective F A Ï† <|VC_injective_function F A Ï† hphi
    )
  )

theorem pow_le_of_shattersâ‚‚ {V : Type*} [DecidableEq V] [Fintype V] (F : Finset (Finset V)) (k : â„•)
    (h : shatters_some F k) : k â‰¤ Nat.log 2 F.card := by
  have := pow_le_of_shatters F k h
  refine (Nat.pow_le_iff_le_log ?hb ?hy).mp this
  simp
  intro hc
  rw [hc] at this
  simp at this


lemma pow_le (m : â„•) : m < 2 ^ m := by
  induction m with
        | zero => simp
        | succ n ih =>
          calc
          n + 1 < 2^n + 1 := by linarith
          _ â‰¤ _ := by ring_nf;linarith

theorem VC_works {V : Type*} [DecidableEq V] [Fintype V] (ğ“• : Finset (Finset V))
    (hğ“• : âˆƒ f, f âˆˆ ğ“•) (k : â„•) :
  k â‰¤ dimVC ğ“• â†” shatters_some ğ“• k := by
  have hemp : ğ“• â‰  âˆ… := nonempty_iff_ne_empty.mp hğ“•
  constructor
  Â· intro h
    apply shatters_monotone ğ“• hğ“• k _ h
    have := Finset.max'_mem (filter (shatters_some ğ“•) (range (#ğ“• + 1)))
      (by
        apply filter_nonempty_iff.mpr
        use 0
        constructor
        Â· simp
        Â· obtain âŸ¨f,hfâŸ© := hğ“•
          exact nonempty_shatters _ hf
      )
    simp at this
    unfold dimVC
    simp_all
  intro hâ‚
  have := le_max' (filter (shatters_some ğ“•) (range (#ğ“• + 1))) k
  unfold dimVC
  simp_all
  apply this
  linarith [pow_le_of_shatters ğ“• k hâ‚, pow_le k]

theorem VC!_works {V : Type*} [DecidableEq V] [Fintype V] (ğ“• : Finset (Finset V))
    (hğ“• : âˆƒ f, f âˆˆ ğ“•) (k : â„•) :
  k â‰¤ dimVC! ğ“• â†” shatters_some ğ“• k := by
  have hemp : ğ“• â‰  âˆ… := nonempty_iff_ne_empty.mp hğ“•
  constructor
  Â· intro h
    apply shatters_monotone ğ“• hğ“• k _ h
    have := Finset.max'_mem (filter (shatters_some ğ“•) (range (#(univ : Finset V) + 1)))
      (by
        apply filter_nonempty_iff.mpr
        use 0
        constructor
        Â· simp
        Â· obtain âŸ¨f,hfâŸ© := hğ“•
          exact nonempty_shatters _ hf
      )
    simp at this
    unfold dimVC!
    simp_all
  intro hâ‚
  have := le_max' (filter (shatters_some ğ“•) (range (#(univ : Finset V) + 1))) k
  unfold dimVC!
  simp_all
  apply this
  show k < #univ + 1
  suffices k â‰¤ #univ by linarith
  obtain âŸ¨t,htâŸ© := hâ‚
  rw [â† ht.1]
  refine card_le_card ?mpr.intro.a
  exact subset_univ t

/-- The VC dimension is bounded by the logarithm of the cardinality.
 This is one of the bounds listed in Wikipedia. -/
theorem pow_le_of_shattersâ‚ƒ {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) (hğ“• : âˆƒ f, f âˆˆ ğ“•) :
    dimVC ğ“• â‰¤ Nat.log 2 ğ“•.card := by
  suffices âˆ€ k, k â‰¤ dimVC ğ“• â†’ k â‰¤ Nat.log 2 ğ“•.card by
    apply this <| dimVC ğ“•
    simp
  intro k hk
  rw [VC_works] at hk
  exact @pow_le_of_shattersâ‚‚ V _ _ ğ“• k hk
  exact hğ“•

theorem pow_le_of_shattersâ‚ƒ! {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) (hğ“• : âˆƒ f, f âˆˆ ğ“•) :
    dimVC! ğ“• â‰¤ Nat.log 2 ğ“•.card := by
  suffices âˆ€ k, k â‰¤ dimVC! ğ“• â†’ k â‰¤ Nat.log 2 ğ“•.card by
    apply this <| dimVC! ğ“•
    simp
  intro k hk
  rw [VC!_works] at hk
  exact @pow_le_of_shattersâ‚‚ V _ _ ğ“• k hk
  exact hğ“•

lemma indexVC!_defined {V : Type*} [DecidableEq V] [Fintype V] {ğ“• : Finset (Finset V)} :
    (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ:Finset V) + 2))).Nonempty := by
  use #(univ : Finset V) + 1
  simp
  unfold shatters_some
  push_neg
  intro A hA
  exfalso
  contrapose hA
  rw [show Fintype.card V = #univ by rfl]
  suffices #A â‰¤ #(univ:Finset V) by
    linarith
  refine card_le_card ?h.a
  exact subset_univ A




lemma indexVC_defined {V : Type*} [DecidableEq V] [Fintype V] {ğ“• : Finset (Finset V)} :
    (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).Nonempty := by
  use #ğ“•
  simp
  intro h
  have := @pow_le_of_shatters V _ _ ğ“• #ğ“• h
  have := pow_le #ğ“•
  linarith

lemma not_shatter {V : Type*} [DecidableEq V] [Fintype V] {ğ“• : Finset (Finset V)} :
    (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).Nonempty := indexVC_defined

lemma not_shatter! {V : Type*} [DecidableEq V] [Fintype V] {ğ“• : Finset (Finset V)} :
    (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))).Nonempty := indexVC!_defined

/-- The VC index is the VC dimension +1 for nonempty finite families, but can be defined for
families of all cardinalities. -/
def indexVC {V : Type*} [DecidableEq V] [Fintype V] (ğ“• : Finset (Finset V)) : â„• :=
  Finset.min' (filter (fun k => Â¬ shatters_some ğ“• k) (range (ğ“•.card + 1))) indexVC_defined


def indexVC! {V : Type*} [DecidableEq V] [Fintype V] (ğ“• : Finset (Finset V)) : â„• :=
  Finset.min' (filter (fun k => Â¬ shatters_some ğ“• k) (range ((univ : Finset V).card + 2))) indexVC!_defined



lemma indexVC_emp {V : Type*} [DecidableEq V] [Fintype V] :
    indexVC (âˆ… : Finset (Finset V)) = 0 := by
      unfold indexVC
      simp
      apply le_antisymm
      Â· have := @min'_le â„• _ (filter (fun k â†¦ Â¬shatters_some (âˆ… : Finset (Finset V)) k) {0})
          0 (by simp;exact empty_does_not_shatter 0)
        aesop
      have := @le_min' â„• _ (filter (fun k â†¦ Â¬shatters_some (âˆ… : Finset (Finset V)) k) {0}) (by
        apply filter_nonempty_iff.mpr
        simp
        exact empty_does_not_shatter 0
      ) 0 (by intro y hy; simp at hy; aesop)
      simp_all

lemma indexVC!_emp {V : Type*} [DecidableEq V] [Fintype V] :
    indexVC! (âˆ… : Finset (Finset V)) = 0 := by
      unfold indexVC!
      simp
      apply le_antisymm
      Â· exact min'_le (filter (fun k â†¦ Â¬shatters_some (âˆ… : Finset (Finset V)) k) (range (Fintype.card V + 2)))
          0 (by simp;exact empty_does_not_shatter 0)
      have := @le_min' â„• _ (filter (fun k â†¦ Â¬shatters_some (âˆ… : Finset (Finset V)) k) {0}) (by
        apply filter_nonempty_iff.mpr
        simp
        exact empty_does_not_shatter 0
      ) 0 (by intro y hy; simp at hy; aesop)
      simp_all


lemma dimVC_emp {V : Type*} [DecidableEq V] [Fintype V] :
    dimVC (âˆ… : Finset (Finset V)) = 0 := by simp [dimVC]

lemma dimVC!_emp {V : Type*} [DecidableEq V] [Fintype V] :
    dimVC! (âˆ… : Finset (Finset V)) = 0 := by simp [dimVC!]



theorem dim_index!  {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) : dimVC! ğ“• = indexVC! ğ“• - 1 := by

  by_cases H : ğ“• = âˆ…
  Â· subst H
    rw [dimVC!_emp,indexVC!_emp]
  Â· have hğ“• : âˆƒ f, f âˆˆ ğ“• := by
      apply Nonempty.exists_mem
      exact nonempty_iff_ne_empty.mpr H

    unfold dimVC! indexVC!
    simp_all
    apply le_antisymm
    Â· apply Nat.le_sub_one_of_lt
      by_contra hc
      simp at hc
      obtain âŸ¨x,hxâŸ© := hc
      obtain âŸ¨y,hyâŸ© := hx.2.2
      exact hx.2.1 <| shatters_monotone ğ“• hğ“• x y hy.2.2 hy.2.1
    Â· apply le_max'
      simp
      constructor
      Â· have hâ‚€ : (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))).min' not_shatter! <
          #(univ : Finset V) + 2 := by
            have := @min'_le â„• _ (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))) (#(univ : Finset V) + 1)
              (by
              simp; unfold shatters_some;push_neg;intro A hA
              exfalso
              contrapose hA
              suffices #A â‰¤ Fintype.card V by linarith
              show #A â‰¤ #univ
              apply card_le_card
              exact subset_univ A
            )
            linarith
        show (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#univ + 2))).min' _ - 1 < #univ + 1
        omega
      Â· by_contra Hâ‚€
        simp at Hâ‚€
        have hâ‚€ : (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))).min' not_shatter! - 1
          < (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))).min' not_shatter! := by
          apply Nat.sub_one_lt
          intro hc
          have : Â¬shatters_some ğ“• 0 := by
            have := min'_mem (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))) not_shatter!
            simp at this
            have := this.2
            simp at *
            rw [hc] at this
            tauto
          exact this <| (equivVC ğ“•).mp <| Nat.zero_lt_of_ne_zero <| card_ne_zero.mpr <| nonempty_iff_ne_empty.mpr H
        have := (@lt_min'_iff â„• _ (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))) not_shatter!
          ((filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))).min' not_shatter! - 1)).mp hâ‚€
          ((filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))).min' not_shatter! - 1)
          (by
            simp
            constructor
            Â· suffices (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))).min' not_shatter! < #(univ : Finset V) + 2 by
                have : Fintype.card V = #(univ : Finset V) := rfl
                simp_rw [this]
                linarith
              have := min'_mem (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#(univ : Finset V) + 2))) not_shatter!
              simp at this
              exact this.1
            exact Hâ‚€
          )
        simp at this


/-- Obtain dimVC from indexVC.
 Since dimVC âˆ… = 0 and indexVC âˆ… x= 0, this relies on 0 - 1 = 0.
-/
theorem dim_index  {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) : dimVC ğ“• = indexVC ğ“• - 1 := by
  by_cases H : ğ“• = âˆ…
  Â· subst H
    rw [dimVC_emp,indexVC_emp]
  Â· have hğ“• : âˆƒ f, f âˆˆ ğ“• := by
      apply Nonempty.exists_mem
      exact nonempty_iff_ne_empty.mpr H

    unfold dimVC indexVC
    simp_all
    apply le_antisymm
    Â· apply Nat.le_sub_one_of_lt
      by_contra hc
      simp at hc
      obtain âŸ¨x,hxâŸ© := hc
      obtain âŸ¨y,hyâŸ© := hx.2.2
      exact hx.2.1 <| shatters_monotone ğ“• hğ“• x y hy.2.2 hy.2.1
    Â· apply le_max'
      simp
      constructor
      Â· suffices (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter < #ğ“• + 1 by
          calc
          _ â‰¤ (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter := by omega
          _ < _ := this
        suffices (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter â‰¤ #ğ“• by linarith
        apply @min'_le â„• _
        simp
        intro h
        linarith [pow_le_of_shatters ğ“• #ğ“• h, pow_le #ğ“•]
      Â· by_contra Hâ‚€
        simp at Hâ‚€
        have hâ‚€ : (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter - 1
          < (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter := by
          apply Nat.sub_one_lt
          intro hc
          have : Â¬shatters_some ğ“• 0 := by
            have := min'_mem (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))) not_shatter
            simp at this
            rw [hc] at this
            exact this.2
          exact this <| (equivVC ğ“•).mp <| Nat.zero_lt_of_ne_zero <| card_ne_zero.mpr <| nonempty_iff_ne_empty.mpr H
        have := (@lt_min'_iff â„• _ (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))) not_shatter
          ((filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter - 1)).mp hâ‚€
          ((filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter - 1)
          (by
            simp
            constructor
            Â· suffices (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))).min' not_shatter < #ğ“• + 1 by
                linarith
              have := min'_mem (filter (fun k â†¦ Â¬shatters_some ğ“• k) (range (#ğ“• + 1))) not_shatter
              simp at this
              exact this.1
            exact Hâ‚€
          )
        simp at this


lemma VC_mono {V : Type*} [DecidableEq V] [Fintype V] (ğ“• ğ“– : Finset (Finset V))
 (hğ“• : âˆƒ f, f âˆˆ ğ“•) (hğ“– : âˆƒ g, g âˆˆ ğ“–)
  (h : ğ“• âŠ† ğ“–) : dimVC ğ“• â‰¤ dimVC ğ“– := by
    suffices âˆ€ k, k â‰¤ dimVC ğ“• â†’ k â‰¤ dimVC ğ“– by
      exact this (dimVC ğ“•) (le_refl _)
    intro k hf
    rw [VC_works] at hf âŠ¢
    obtain âŸ¨A,hAâŸ© := hf
    use A
    simp_all
    intro B hB
    obtain âŸ¨C,hCâŸ© := hA.2 B hB
    use C
    tauto
    exact hğ“–
    exact hğ“•

lemma VC!_mono {V : Type*} [DecidableEq V] [Fintype V] (ğ“• ğ“– : Finset (Finset V))
  (h : ğ“• âŠ† ğ“–) : dimVC! ğ“• â‰¤ dimVC! ğ“– := by
    by_cases hğ“• : âˆƒ f, f âˆˆ ğ“•
    have hğ“– : âˆƒ g, g âˆˆ ğ“– := by aesop
    suffices âˆ€ k, k â‰¤ dimVC! ğ“• â†’ k â‰¤ dimVC! ğ“– by
      exact this (dimVC! ğ“•) (le_refl _)
    intro k hf
    rw [VC!_works] at hf âŠ¢
    obtain âŸ¨A,hAâŸ© := hf
    use A
    simp_all
    intro B hB
    obtain âŸ¨C,hCâŸ© := hA.2 B hB
    use C
    tauto
    exact hğ“–
    exact hğ“•
    Â· have : ğ“• = âˆ… := by aesop
      subst this
      rw [show dimVC! âˆ… = 0 by rfl]
      simp

lemma VC_trivBound  {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) (hğ“• : âˆƒ f, f âˆˆ ğ“•) :
    dimVC ğ“• â‰¤ (univ : Finset V).card := by
  suffices âˆ€ k, k â‰¤ dimVC ğ“• â†’ k â‰¤ (univ : Finset V).card by
    exact this (dimVC ğ“•) (le_refl _)
  intro k hk
  rw [VC_works] at hk
  obtain âŸ¨A,hAâŸ© := hk
  rw [â† hA.1]
  exact card_le_univ A
  exact hğ“•

lemma VC!_trivBound  {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V)) (hğ“• : âˆƒ f, f âˆˆ ğ“•) :
    dimVC! ğ“• â‰¤ (univ : Finset V).card := by
  suffices âˆ€ k, k â‰¤ dimVC! ğ“• â†’ k â‰¤ (univ : Finset V).card by
    exact this (dimVC! ğ“•) (le_refl _)
  intro k hk
  rw [VC!_works] at hk
  obtain âŸ¨A,hAâŸ© := hk
  rw [â† hA.1]
  exact card_le_univ A
  exact hğ“•

/-- Kathleen Romanik's testing dimension, index version. -/
def indexTest {V : Type*} [DecidableEq V] [Fintype V] (ğ“• : Finset (Finset V))
  (hf : âˆƒ B, B âˆ‰ ğ“•) : â„• :=
    Finset.min' (filter (fun k => Â¬ shatters_all ğ“• k) (range ((univ:Finset V).card + 1))) (by
  unfold shatters_all
  push_neg
  simp
  use #(univ : Finset V)
  simp
  use univ
  simp
  tauto
)

/-- Kathleen Romanik's testing dimension. -/
def dimTest {V : Type*} [DecidableEq V] [Fintype V] (ğ“• : Finset (Finset V)) : â„• := by
  by_cases H : ğ“• = âˆ…
  Â· exact 0
  Â·
    have : âˆƒ f, f âˆˆ ğ“• := by
      refine Nonempty.exists_mem ?h
      exact nonempty_iff_ne_empty.mpr H
    exact Finset.max' (filter (fun k => shatters_all ğ“• k) (range ((univ:Finset V).card + 1))) (by
    use 0
    simp
    intro A hA B hB
    obtain âŸ¨t,htâŸ© := this
    use t
    constructor
    tauto
    have : A = âˆ… := by aesop
    have : B = âˆ… := by
      apply subset_empty.mp
      simp_all
    aesop
  )


/-- The VC dimension of the powerset is the cardinality of the underlying set.
 Note that this does not require [Nonempty V]. -/
lemma dimVC_powerset  {V : Type*} [DecidableEq V] [Fintype V] :
    dimVC (univ : Finset (Finset V)) =
           (univ : Finset V).card := by
  suffices âˆ€ k, k â‰¤ dimVC (univ : Finset (Finset V)) â†” k â‰¤ (univ : Finset V).card by
    apply le_antisymm
    apply (this (dimVC (univ : Finset (Finset V)))).mp
    simp
    rw [VC_works]
    use Finset.univ
    simp
    use univ
    simp
  intro k
  constructor
  Â· intro h
    calc
    _ â‰¤ _ := h
    _ â‰¤ _ := VC_trivBound (univ : Finset (Finset V)) (by use univ;simp)
  Â· intro h
    rw [VC_works]
    apply shatters_monotone
    use univ
    simp
    exact h
    use Finset.univ
    simp
    use univ
    simp

lemma dimVC!_powerset  {V : Type*} [DecidableEq V] [Fintype V] :
    dimVC! (univ : Finset (Finset V)) =
           (univ : Finset V).card := by
  suffices âˆ€ k, k â‰¤ dimVC! (univ : Finset (Finset V)) â†” k â‰¤ (univ : Finset V).card by
    apply le_antisymm
    apply (this (dimVC! (univ : Finset (Finset V)))).mp
    simp
    rw [VC!_works]
    use Finset.univ
    simp
    use univ
    simp
  intro k
  constructor
  Â· intro h
    calc
    _ â‰¤ _ := h
    _ â‰¤ _ := VC!_trivBound (univ : Finset (Finset V)) (by use univ;simp)
  Â· intro h
    rw [VC!_works]
    apply shatters_monotone
    use univ
    simp
    exact h
    use Finset.univ
    simp
    use univ
    simp



lemma dimVC_eq  {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V))
    (hğ“• : âˆƒ f, f âˆˆ ğ“• )
    (k : â„•) :
    @shatters_some V _ ğ“• k âˆ§ Â¬ @shatters_some V _  ğ“• (k + 1) â†’ dimVC ğ“• = k := by
  intro âŸ¨hâ‚€,hâ‚âŸ©
  rw [â† VC_works _ hğ“•] at hâ‚€ hâ‚
  linarith

lemma dimVC!_eq  {V : Type*} [DecidableEq V] [Fintype V]
    (ğ“• : Finset (Finset V))
    (hğ“• : âˆƒ f, f âˆˆ ğ“• )
    (k : â„•) :
    @shatters_some V _ ğ“• k âˆ§ Â¬ @shatters_some V _  ğ“• (k + 1) â†’ dimVC! ğ“• = k := by
  intro âŸ¨hâ‚€,hâ‚âŸ©
  rw [â† VC!_works _ hğ“•] at hâ‚€ hâ‚
  linarith


open Nat

section clog
theorem rescue_lemma_12 {m : â„•} : (2 * m.succ).succ < 2 ^ (clog 2 (2 * m.succ).succ) := by
  cases (lt_or_eq_of_le (le_pow_clog one_lt_two _)) with
  | inl g => exact g
  | inr g =>
    exfalso
    obtain âŸ¨k,hkâŸ© : âˆƒ k, clog 2 (succ (2 * m.succ)) = succ k :=
      exists_eq_succ_of_ne_zero <| pos_iff_ne_zero.mp <| clog_pos one_lt_two <| by linarith
    have : 2 ^ clog 2 (succ (2 * m.succ)) = 2 * 2^ (clog 2 (succ (2 * m.succ)) - 1) := by
      rw [hk, pow_succ]
      simp
      ring
    have hâ‚€: Even ((2 * m.succ).succ) := g â–¸ this â–¸ even_two_mul _
    simp at hâ‚€

end clog

section Î½â‚‚

/-- Î½â‚‚ is called t in the paper.  -/
def Î½â‚‚ := padicValNat 2


theorem Î½â‚‚_odd (n:â„•) : Î½â‚‚ (2 * n+1) = 0 :=
  padicValNat.eq_zero_of_not_dvd fun âŸ¨k,hkâŸ© =>
    (not_even_iff_odd).mpr (hk â–¸ odd_two_mul_add_one n) <|even_two_mul k

theorem Î½â‚‚_2 (n:â„•) (h:0<n) : Î½â‚‚ (2 * n) = Î½â‚‚ n + 1:= by
  rw [Î½â‚‚, padicValNat.mul (by linarith) (by linarith), add_comm]
  simp

end Î½â‚‚

section w

theorem bits_odd {n:â„•} : bits (2 * n+1) = true :: bits n :=
  bits_append_bit n true (fun _ => rfl) â–¸ rfl

theorem bits_even {n:â„•} (h : n â‰  0) : bits (2 * n) = false :: bits n :=
  bits_append_bit n false (False.elim âˆ˜ h) â–¸ rfl

theorem length_map_sum : âˆ€ l : List Bool, (l.map Bool.toNat).sum â‰¤ l.length
| [] => by simp
| head :: tail => by rw [List.length_cons,List.map_cons,List.sum_cons]; calc
  _ â‰¤ 1 + List.sum (List.map Bool.toNat tail) := add_le_add_right (Bool.toNat_le head) _
  _ â‰¤ 1 + List.length tail                    := add_le_add_left (length_map_sum _) _
  _ = succ (List.length tail)                 := by linarith

-- w = binaryHammingWeight (Lemma 11 of VC paper)
def hammingBits (n : â„•) : â„• := ((bits n).map Bool.toNat).sum

theorem hammingBits_2 (n:â„•) : hammingBits (2 * n) = hammingBits n := by
  by_cases h : n = 0
  Â· simp [h]
  Â· unfold hammingBits
    rw [bits_even h]
    unfold Bool.toNat
    simp

theorem hammingBits_odd (n:â„•) : hammingBits (2 * n+1) = hammingBits n + 1 := by
  unfold hammingBits
  rw [bits_odd]
  simp [add_comm]

theorem mentioned_in_lemma_12 {n s : â„•} (h : n < 2^s) : hammingBits n â‰¤ s := calc
  _ â‰¤ List.length (bits n) := length_map_sum _
  _ â‰¤ _                    := by rw [size_eq_bits_len,size_le];exact h

theorem andhence {m : â„•} : hammingBits (2 * m.succ) < clog 2 (2 * m.succ).succ := calc
  _ < hammingBits (2 * m.succ + 1) := by rw [hammingBits_odd,hammingBits_2]; simp
  _ â‰¤ _                            := mentioned_in_lemma_12 rescue_lemma_12

end w

theorem Î½â‚‚_hammingBits (n : â„•) : Î½â‚‚ (n + 1) + hammingBits (n + 1) = hammingBits n + 1 := by
  induction' n using Nat.strong_induction_on with n ih -- https://leanprover-community.github.io/mathematics_in_lean/mathematics_in_lean.pdf
  by_cases hn : n = 0
  Â· simp [hn, Î½â‚‚, hammingBits]
  by_cases he : Even n
  Â· obtain âŸ¨k,hkâŸ© := he
    have hâ‚ : 0 < k := Nat.pos_of_ne_zero fun _ => hn <| by linarith
    have hâ‚‚ : k < n := calc k < k + k := add_lt_add_left hâ‚ k
                            _ = n     := hk.symm
    have : hammingBits n = hammingBits k := by rw [â† hammingBits_2 k, two_mul, hk]
    rw [this, â† ih k hâ‚‚] -- use ind.hyp.
    subst hk
    apply succ_injective
    conv =>
      right
      rw [succ_eq_add_one, add_assoc]
    rw [â† two_mul,hammingBits_odd k, â† ih k hâ‚‚, Î½â‚‚_odd _]
    linarith

  Â· obtain âŸ¨k,hkâŸ© := not_even_iff_odd.mp he
    subst hk
    rw [
      show 2 * k + 1 + 1 = 2 * (k + 1) by ring,
      hammingBits_2, hammingBits_odd, Î½â‚‚_2, add_comm, â† add_assoc
    ]
    apply (add_left_inj _).mpr
    rw [add_comm, ih k] -- use ind.hyp.
    linarith
    linarith

/-- This function is called `a` at https://oeis.org/A005187 and we
name it in honor of JÃ¶rg Arndt. -/
def arndt : â„• â†’ â„•
| 0 => 0
| succ k => arndt k + 1 + Î½â‚‚ (k+1)

/-- JÃ¶rg Arndt (2019) claimed this without proof
  as a resolution of a conjecture of Benoit Cloitre (2003).
  Cloitre is a coauthor of Shallit (2023) and Jean-Paul Allouche.
-/
lemma lemma11 : âˆ€ m, arndt m + hammingBits (2 * m) = 2 * m
| 0 => by
  simp
  Â· constructor
    Â· rfl
    Â· simp [hammingBits]
| succ n => add_right_cancel <|
  calc
    _ = arndt n + 1 + Î½â‚‚ n.succ + hammingBits (2*(n.succ))+ hammingBits (2 * n)  := rfl
    _ = arndt n + hammingBits (2 * n) + 1 + Î½â‚‚ n.succ + hammingBits (2 * n.succ) := by ring
    _ = (2 * n)         + 1 + Î½â‚‚ n.succ + hammingBits (2 * n.succ) := by rw [lemma11 n]
    _ = (2 * n) + 1 + (Î½â‚‚ n.succ + hammingBits (2 * n.succ))       := by ring
    _ = (2 * n) + 1 + (hammingBits (2 * n) + 1) := by rw [hammingBits_2, hammingBits_2, Î½â‚‚_hammingBits]
    _ = _                                       := by linarith

lemma lemma12 (n:â„•) : arndt n + clog 2 n.succ â‰¥ 2 * n := by
  by_cases h: n = 0
  Â· subst h
    simp
  obtain âŸ¨m,hmâŸ© := exists_eq_succ_of_ne_zero h
  subst hm
  have : 2 â‰¤ (2 * m.succ).succ := le_of_ble_eq_true rfl
  have hkey: clog 2 (succ m.succ) + 1 â‰¥ clog 2 (succ (2 * m.succ)) := by
    rw [clog_of_two_le one_lt_two this] -- strange but wonderful!
    simp
  have : arndt m.succ + clog 2 (succ m.succ) + 1 > 2 * m.succ := calc
     _ â‰¥ arndt m.succ + clog 2 (succ (2 * m.succ)) := add_le_add_left hkey _
     _ > arndt m.succ + hammingBits (2 * m.succ)   := add_lt_add_left (@andhence m) _
     _ = 2 * m.succ                                := lemma11 m.succ
  exact lt_succ.mp this
